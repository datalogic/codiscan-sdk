plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

// Current major and minor version of the Codiscan Client.  When built in GitLab
// the name of the tag is used as the version number.  Otherwise, the version
// defaults to 9.9.9 for local Android builds.
String VERSION_NUM = System.getenv("CI") ? System.getenv("CI_COMMIT_TAG") : "9.9.9"

android {
    namespace 'com.datalogic.codiscan'
    compileSdk 33

    defaultConfig {
        minSdk 33
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // Specify what to include in the artifacts: sources JAR.
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Define Release publication.
            release(MavenPublication) {
                // Specify what build variant to use: release.
                from components.release

                // Define publishing identify values.
                groupId = 'com.github.datalogic'
                artifactId = 'codiscan'
                version = VERSION_NUM
                // Specify the POM metadata.
                pom {
                    name = "CODiScan SDK"
                    description = "SDK to integrate a CODiScan device with a mobile device."
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation project (":codiscanlibrary")
}
