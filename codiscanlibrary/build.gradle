plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'kotlin-parcelize'
}

// Current major and minor version of the Codiscan Client.  When built in GitLab
// the name of the tag is used as the version number.  Otherwise, the version
// defaults to 9.9.9 for local Android builds.
String VERSION_NUM = System.getenv("CI") ? System.getenv("CI_COMMIT_TAG") : "9.9.9"

android {
    namespace 'com.datalogic.codiscansdk'
    compileSdk 34

    defaultConfig {
        minSdk 26
        targetSdk 34
        versionCode 2
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        aidl true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Define Release publication.
            release(MavenPublication) {
                // Specify what build variant to use: release.
                from components.release

                // Define publishing identify values.
                groupId = 'com.github.datalogic'
                artifactId = 'codiscanlibrary'
                version = VERSION_NUM
                // Specify the POM metadata.
                pom {
                    name = "CODiScan AIDL"
                    description = "AIDL interface between the CODiScan Service and CODiScan SDK."
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
}
